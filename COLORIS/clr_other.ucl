// This is the common module for the normal NRU cache design
// It contains all the types and constants that are used in the design
module common {
    type time_t = bv4;
    type addr_t = bv32;
    type ind_t  = bv8;//256 direct
    type clr_t  = bv4;
    type tag_t  = bv22;
    type way_t  = bv2;
    type map_t  = [way_t] boolean;

    type userRequest_t = record {
        valid: boolean,
        address: addr_t
    };
    type cacheLine_t = record {
        tag: tag_t,
        valid: boolean
    };
    type cacheWays_t = [ind_t]cacheLine_t;
}

// This is the main module for the cache
module cache {
    // Import all the common definitions
    type * = common.*;

    const way_low   : way_t = 0bv2;
    const way_high  : way_t = 3bv2;

    const index_low     : ind_t = 0bv8;
    const index_high    : ind_t = 255bv8;
    
    // Input
    input userRequest: userRequest_t;

    // Main part of the cache
    var cache: [way_t]cacheWays_t;
	var metadata: [ind_t]map_t;

    // Temporary variables
    var metadataLine : [bv3] boolean;
    var updateLineMetadata: [bv3] boolean;

    var victimWay : way_t;
    
    // This part is virtual data (temporary variables)
    var hitLine : ind_t;
    var hit : boolean;
    var hitWays : way_t;
    var hitTag : tag_t;
    var reqTag  : tag_t;
    var address  : addr_t;

    // All zero cacheline and cacheway
    const null_cacheline    : cacheLine_t;
    const null_cacheway     : cacheWays_t;
    const null_map         : map_t;

    // Convert from address (bv64) to an index in the cache (bv5)
    // Drop the lower 3-bits of offset and take the rest
    define addr_to_index (addr: bv32) : ind_t = addr[9:2];
    // Convert from address to the the tag 
    // Top 56 bits
    define addr_to_tag (addr: bv32) : tag_t = addr[31:10];

    // Uninterpreted function (any definition is possible)
    function f_update_hit (hit_way: way_t, prev_mdata: map_t) : map_t;
    function f_update_miss (hit_way: way_t, prev_mdata: map_t) : map_t;

    // ======================================================
    // External requests/actions
    // ======================================================


    // initialize the cache
    procedure initializeCache ()
        modifies cache, metadata, hit;
    {
        hit = false;
        for (i: way_t) in range(way_low, way_high) {
            cache[i] = null_cacheway;
        }
        for (j: ind_t) in range(index_low, index_high){
            metadata[j] = null_map;
        }
    }

    procedure serveUserRequest (userreq : userRequest_t)
        modifies metadata,cache;
        modifies victimWay,updateLineMetadata;
        modifies hit,hitWays,metadataLine,hitLine,hitTag,reqTag;
        modifies address;
    {
        address = userreq.address;
        hitLine = addr_to_index(userreq.address);
        reqTag = addr_to_tag(userreq.address);

        hit = false;
        for (way_id : way_t) in range(way_low, way_high) {
             if (cache[way_id][hitLine].tag == reqTag && cache[way_id][hitLine].valid) {
                hitTag = cache[way_id][hitLine].tag;
                hit = true;
                hitWays = way_id;
            }
        }
        if (hit) {
            call updateNRUTree_hit (hitWays, hitLine);
        } else {
            call (victimWay) = findVictim(hitLine);
            call updateNRUTree_miss (victimWay, hitLine);
            call writeCache(victimWay,hitLine,userreq.address);
        }
    }

    procedure updateNRUTree_hit (hit_ways: way_t, hit_line: ind_t) returns ()
        modifies metadata;
    {
        // var temp_metadata: map_t;
        // temp_metadata = metadata[hit_line];

        // temp_metadata[hit_ways] = true;

        metadata[hit_line] = f_update_hit(hit_ways, metadata[hit_line]);
    }

    procedure updateNRUTree_miss (hit_ways: way_t, hit_line: ind_t) returns ()
        modifies metadata;
    {
        // var temp_metadata: map_t;
        // temp_metadata = metadata[hit_line];

        // temp_metadata[hit_ways] = true;

        metadata[hit_line] = f_update_miss(hit_ways, metadata[hit_line]);
    }

    procedure updateNRUTree(hit_ways: way_t, hit_line: ind_t) returns ()
        modifies metadata;
    {
        var temp_metadata: map_t;
        temp_metadata = metadata[hit_line];

        temp_metadata[hit_ways] = true;

        metadata[hit_line] = temp_metadata;
    }

    procedure findVictim (hit_line: ind_t) returns (victimWay: way_t)
        // modifies metadata;
    {
        var temp_metadata: map_t;
        victimWay = 0bv2;

        temp_metadata = metadata[hit_line];
        
        for (i: way_t) in range(way_low, way_high) {
            //if one way is not accessed
            if(!temp_metadata[i]){
                victimWay = i;
            }
        }

        // metadata[hit_line] = null_map;
    }
    
    procedure writeCache(victimWay: way_t, hitLine: ind_t,address: addr_t)
    modifies cache,hitLine;
    {
        var virtualCacheWay : cacheWays_t;
        var virtualCacheLine : cacheLine_t;
        var reqtag : tag_t;

        reqtag = addr_to_tag(address);

        virtualCacheWay = cache[victimWay];
        virtualCacheLine = virtualCacheWay[hitLine];
        
        virtualCacheLine.tag = reqtag;
        virtualCacheLine.valid = true;

        virtualCacheWay[hitLine] = virtualCacheLine;
        cache[victimWay] = virtualCacheWay;
    }

    init {
        // Initial cache state is unconstrained, so can we check
        assume (null_cacheline == const_record(tag := 0bv22,valid :=false));
        assume (null_cacheway == const(null_cacheline, [ind_t]cacheLine_t));
        assume (null_map == const(false, [way_t]boolean));
        //call initializeCache();
    }


	next {
        if (userRequest.valid) {
            call serveUserRequest(userRequest);
        }
    }
}

module main {
    type * = common.*;

    var addr_0 : userRequest_t;
    var addr_1 : userRequest_t;
    var clrAddr: addr_t;
    var visitAddr_0: addr_t;
    var visitAddr_1: addr_t;  
    
    const index_low     : ind_t = 0bv8;
    const index_high    : ind_t = 255bv8;
    
    const way_low   : way_t = 0bv2;
    const way_high  : way_t = 3bv2;

    // DAWG instances
    instance cache_0 : cache(
        userRequest:    (addr_0)
    );

    instance cache_1 : cache(
        userRequest:    (addr_1)
    );

    procedure assumeInvariantClrData()
        modifies cache_0,cache_1;
    {
        var index_clr: clr_t;
        var clr: clr_t;
        var cacheLineVar: cacheLine_t;
        var tempCacheWay_0: cacheWays_t;

        assume(cache_0.hit == cache_1.hit);
        
        clr = clrAddr[9:6];

        for (way_id : way_t) in range(way_low, way_high) {
            for( index_id: ind_t) in range(index_low,index_high){
                index_clr = index_id[7:4];
                if(clr == index_clr){
                    assume(cache_0.cache[way_id][index_id]==cache_1.cache[way_id][index_id]);
                    assume(cache_0.metadata[index_id]==cache_1.metadata[index_id]);
                }
                    
            }
        }
    }

    procedure assertInvariantClrData()
        modifies cache_0,cache_1;
    {
        var index_clr: clr_t;
        var clr: clr_t;
        var cacheLineVar: cacheLine_t;
        var tempCacheWay_0: cacheWays_t;

        clr = clrAddr[9:6];

        for (way_id : way_t) in range(way_low, way_high) {
            for( index_id: ind_t) in range(index_low,index_high){
                index_clr = index_id[7:4];
                if(clr == index_clr){
                    assert(cache_0.cache[way_id][index_id]==cache_1.cache[way_id][index_id]);
                    assert(cache_0.metadata[index_id]==cache_1.metadata[index_id]);
                }
                    
            }
        }
    }

    procedure doOneStep()
        modifies addr_0,addr_1;
    {
        addr_0.valid = true;
        addr_0.address = visitAddr_0;

        addr_1.valid = true;
        addr_1.address = visitAddr_1;
    }

    init {
        //asume that user1 have access clrAddr
        //so, the user2 cannot access the same color
        havoc clrAddr;
        havoc visitAddr_0;
        havoc visitAddr_1;
        assume(visitAddr_0[9:6]!=clrAddr[9:6]);
        assume(visitAddr_1[9:6]!=clrAddr[9:6]);
        addr_0.valid = false;
        addr_1.valid = false;
        call assumeInvariantClrData();
    }

    next {
        call doOneStep();
        next (cache_0);
        next (cache_1);
        call assertInvariantClrData();
    }
    //property my_prop: (cache_0.hit == cache_1.hit);

    control {
        v = unroll(2);
        check;
        v.print_cex(
            cache_0.hit,
            cache_0.hitLine,
            cache_0.hitTag,
            cache_1.cache,

            cache_1.hit,
            cache_1.hitLine,
            cache_1.hitTag
        );
    }
}